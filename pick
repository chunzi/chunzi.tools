#!/usr/bin/env perl
use strict;
use warnings;
our $VERSION = '0.01';

use File::Copy::Recursive qw/ dircopy /;
use File::HomeDir;
use Getopt::Long::Descriptive;
use Path::Class;

my $default_from = "/Volumes/500GB/Cloud/Music";

#-----------------------
# options / usages
my ( $opt, $usage ) = describe_options(
    '%c %o',
    [ 'just=i',  "number of albums to pick, default is 18"],
    [ 'from=s',  "the music album dir, default is $default_from" ],
    [ 'help',    "print usage message and exit" ],
);
if ( $opt->help ){ print $usage->text; exit; }


#-----------------------
# resources
my $just = $opt->just || 18; 
my $from = dir( $opt->from || $default_from );
die "Album dir not found: $from\n" unless -d $from;

my $itunes_auto = dir( File::HomeDir->my_home, 'Music', 'iTunes', 'iTunes Media', 'Automatically Add to iTunes');
die "iTunes Music Auto Add dir not found: $itunes_auto\n" unless -d $itunes_auto;

my $itunes_music = dir( File::HomeDir->my_home, 'Music', 'iTunes', 'iTunes Media', 'Music');
if ( -d $itunes_music ){ 
    my @existing = $itunes_music->children;
    die "$itunes_music is not empty.\n" if scalar @existing;
}


#-----------------------
# new pick
my @albums = grep { -d } $from->children;
my $count = scalar @albums;
my $picked; $picked->{int rand($count)}++ while keys %$picked < $just;

#-----------------------
# copy to itunes auto add dir
foreach my $picked ( @albums[keys %$picked] ) {
    my $basename = $picked->dir_list(-1);
    my $target = $itunes_auto->subdir( $basename );
    print "$basename\n";
    dircopy( "$picked", "$target" );
}

exit(0);
