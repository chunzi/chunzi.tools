#!/usr/bin/env perl
use strict;
use warnings;
our $VERSION = '0.01';

use Class::Date qw/ -DateParse gmdate /;
use Config::GitLike::Git;
use Data::Dumper;
use Getopt::Long::Descriptive;
use HTTP::Request::Common;
use IPC::Cmd qw/ can_run /;
use JSON::Syck qw/ Load /;
use MIME::Base64;
use Path::Class;
use Term::ANSIColor qw/ :constants /;
use WWW::Mechanize::GZip;
use YAML::Syck qw/ LoadFile DumpFile /;

local $Term::ANSIColor::AUTORESET = 1;

#-----------------------
# check git installed or not
my $git = can_run("git") || die "no git found.\n";

#-----------------------
# options / usages
my ( $opt, $usage ) = describe_options(
    '%c %o',
    [ 'login=s',  "the login name"],
    [ 'token=s',  "the login token" ],
    [ 'target=s', "the directory to backup, default is current directory", ],
    [ 'help',     "print usage message and exit" ],
);
if ( $opt->help ){ print $usage->text; exit; }


#-----------------------
# try load login/token from gitconfig file
my $login = $opt->login;
my $token = $opt->token;
if ( not defined $login ){
    my $conf = new Config::GitLike::Git; $conf->load;
    $login = $conf->get( key => 'github.user' );
    $token = $conf->get( key => 'github.token' );
}
# backup to target directory, load the check file
my $target = dir( $opt->target || '.' )->absolute;
-d $target or $target->mkpath( 0, 0777 );
my $checkfile = $target->file('.github-backup')->stringify;
my $check = -f $checkfile ? LoadFile($checkfile) : {};


#-----------------------
# wget the repos, check and update
my @repos = wget_repos( $login, $token );

# attach date object
map { $_->{'date'} = gmdate( $_->{'pushed_at'} ) } @repos;

# found the local git repos, some may removed from github, note the user later
my $gitdir_local = {};
map { $gitdir_local->{$_->stringify}++ } grep { /\.git$/ } grep { -d } $target->children;


#-----------------------
# check the repos, newly the first
for my $repos ( sort { $b->{'date'} <=> $a->{'date'} } @repos ){

    my ( $date, $name ) = map { $repos->{$_} } qw/ date name /;
    my $url = sprintf 'git@github.com:%s/%s.git', $login, $name;
    my $epoch = $date->epoch;
    my $gitdir = $target->subdir("$name.git")->absolute;

    delete $gitdir_local->{"$gitdir"};

    print STDERR MAGENTA "==> "; 
    print STDERR GREEN "$url";
    print STDERR "\n";

    my $updated = 1 if exists $check->{$url} and $check->{$url} == $epoch;
    my $exists = 1 if -d $gitdir;
    
    if ( not $updated or not $exists ){
        # alreay cloned, just get into it and git remote update
        if ( -d $gitdir ){
            chdir "$gitdir" or die "Can't cd to $gitdir: $!\n";
            system("$git remote update") == 0 or die "Failed to execute git remote update: $?\n";

        # or just clone it as mirrored repos
        }else{
            chdir "$target" or die "Can't cd to $target: $!\n";
            system("$git clone --mirror $url $gitdir") == 0 or die "Failed to execute git clone: $?\n";
        }
        $check->{$url} = $epoch;
        DumpFile($checkfile, $check);
    }
}

#-----------------------
# Show the removed repos at Github
for my $path ( sort keys %$gitdir_local ){
    print STDERR MAGENTA "==> Removed from Github: "; 
    print STDERR GREEN "$path";
    print STDERR "\n";
}
exit(0);



#-----------------------
sub wget_repos { 
    my $login = shift; 
    my $token = shift;
    my $url = shift; 

    $url ||= sprintf 'http://github.com/api/v2/json/repos/show/%s', $login;
    my $basic = sprintf "%s/token:%s", $login, $token;
    my $auth = sprintf 'Basic %s', encode_base64($basic);

    my $req = HTTP::Request::Common::GET( $url );
    $req->header( 'Authorization', $auth );

    my $mech = new WWW::Mechanize::GZip;
    my $res = $mech->request( $req );
    die "login error!\n" if $res->code == 404;

    my $json = $res->content();
    my $hash = Load( $json );
    my @repos = @{$hash->{'repositories'}};

    my $next = $res->header('x-next');
    if ( $next ){
        my @next_repos = wget_repos( $login, $token, $next );
        push @repos, @next_repos;
    }

    return @repos;
}

exit;
