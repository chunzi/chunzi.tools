#!/usr/bin/env perl
use strict;
use warnings;
our $VERSION = '0.02';

use LWP::Simple;
use LWP::Protocol::https;
use JSON::Syck;
use Encode;
use Encode::JavaScript::UCS;
use Class::Date qw/ date /;
use Path::Class;
use File::HomeDir;
use Getopt::Long::Descriptive;
use File::Slurp;

binmode STDOUT, ":utf8";
local $| = 1;


#---------------------------
# options / usage
my ( $opt, $usage ) = describe_options(
    '%c %o',
    [ 'client_id=s',     "the login name"],
    [ 'access_token=s',  "the login token" ],
    [ 'target=s',        "the directory to backup, default is current directory", ],
    [ 'help',            "print usage message and exit" ],
);
if ( $opt->help ){ print $usage->text; exit; }


#---------------------------
# user / token from cmd options
my $client_id = $opt->client_id;
my $access_token = $opt->access_token;

# or just from ~/.instagram file
# first line is the client_id and the second line is the access token
my $conf_file = file( home(), '.instagram' )->stringify;
if ( defined $client_id and defined $access_token ){
    write_file( $conf_file, "$client_id\n$access_token\n" );
}else{
    ( $client_id, $access_token ) = grep { chomp } read_file( $conf_file ) if -f $conf_file;
    die "Usage: $0 --client_id CLIENT_ID --access_token TOKEN\n"
        unless ( defined $client_id and defined $access_token );
}


#---------------------------
# which directory to backup, default is current directory
my $target = dir( $opt->target || '.' );
-d $target or $target->mkpath( 0, 0777 );


#---------------------------
# well, let's backup my instagram photos
backup();
exit(0);


#---------------------------
sub backup {
    my $url = shift;

    # start point
    $url ||= "https://api.instagram.com/v1/users/self/media/recent/?access_token=$access_token";
    printf "Loading %s ...\n", $url;


    my $json = get( $url ); 
    die "Couldn't get the json from the API!\n" unless defined $json;
    my $hash = JSON::Syck::Load( $json );

    my $keep_loading = 0;
    my @photos = ();
    for ( @{$hash->{'data'}} ){
        my $id = $_->{'id'};

        # seems sometimes the id appears as dddddd_88653 again and again,
        # simply ignore those invalid ids
        next if $id =~ /\D/;

        my $date = date $_->{'created_time'};
        my $url = $_->{'images'}{'standard_resolution'}{'url'};
        my $title = decode("JavaScript-UCS", $_->{'caption'}{'text'} || 'Untitled' );
        my $filename = sprintf "%s - %04d-%02d-%02d - %s.jpg", $id, $date->year, $date->month, $date->day, $title;
        my $file = $target->file( $filename )->stringify;
        if ( -f $file ){
            printf "    %s\n", $filename;
        }else{
            printf "  A %s\n", $filename;
            getstore( $url, $file );
            $keep_loading = 1;
        }
    }

    if ( $keep_loading ){
        my $next_url = $hash->{'pagination'}{'next_url'};
        return unless defined $next_url;
        backup( $next_url );
    }else{
        print "END\n";
    }
}



